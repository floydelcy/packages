_commit=dc8323741043a75537fb770d42ca37498a3d8947
pkgname=janus-gateway-pikvm
pkgver=1.3.0
pkgrel=4
pkgdesc="An open source, general purpose, WebRTC server (PiKVM build)"
url='https://janus.conf.meetecho.com'
license=(GPL3)
arch=(armv6h armv7h aarch64)
depends=(
	glib2
	jansson
	libconfig
	libwebsockets
	libnice
	libsrtp
	openssl
)
makedepends=(
	cmake
	gcc
	pkg-config
	gengetopt
	autoconf
)
conflicts=(
	janus-gateway
	janus-gateway-git
)
provides=(
	janus-gateway
)
source=(
#	"$pkgname-$pkgver.tar.gz::https://github.com/meetecho/janus-gateway/archive/v$pkgver.tar.gz"
	"$pkgname-$_commit.tar.gz::https://github.com/meetecho/janus-gateway/archive/$_commit.tar.gz"
	"https://webrtc.github.io/adapter/adapter-latest.js"
)
md5sums=(SKIP SKIP)


build() {
#	cd "janus-gateway-$pkgver"
	cd "janus-gateway-$_commit"
	./autogen.sh
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--disable-docs \
		--disable-data-channels \
		--disable-turn-rest-api \
		--disable-all-plugins \
		--disable-all-loggers \
		--disable-all-transports \
		--enable-websockets \
		--disable-sample-event-handler \
		--disable-websockets-event-handler \
		--disable-gelf-event-handler
	make
}

package() {
#	cd "janus-gateway-$pkgver"
	cd "janus-gateway-$_commit"
	make DESTDIR="$pkgdir" install
	mkdir "$pkgdir/usr/lib/janus/loggers"
	sed -i -e 's|^#include "refcount.h"$|#include "../refcount.h"|g' "$pkgdir/usr/include/janus/plugins/plugin.h"
	sed -i -e 's|^var Janus = (|export var Janus = (|g' "$pkgdir/usr/share/janus/javascript/janus.js"
	sed -i '2s|^|import "./adapter.js"\n|' "$pkgdir/usr/share/janus/javascript/janus.js"
	cp ../adapter-latest.js "$pkgdir/usr/share/janus/javascript/adapter.js"
}
